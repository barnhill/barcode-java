apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'maven-publish'

wrapper {
    //if you change this version you will need to run the 'gradlew wrapper' command twice from the command line to auto update the gradle cache
    gradleVersion = "6.8.2"
}

group 'com.pnuema.java'
version '1.11'

jar {
    manifest {
        attributes 'Implementation-Title': 'Barcode for Java',
                   'Implementation-Version': archiveVersion,
                   'Implementation-Vendor': 'Brad Barnhill'
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.13.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives jar, sourcesJar, javadocJar
}

ext {
    baseUrl = 'https://github.com/barnhill'
    siteUrl = baseUrl + '/barcode-java'
    gitUrl = baseUrl + '/barcode-java.git'
    descr = 'Barcode image generation library in Java'
}

signing {
    sign configurations.archives
}

sonatypeUser = System.getenv("SONATYPE_USER") != null ? System.getenv("SONATYPE_USER") : SONATYPE_USER
sonatypePass = System.getenv("SONATYPE_PASS") != null ? System.getenv("SONATYPE_PASS") : SONATYPE_PASS

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            pom.project {
                name 'Barcode-Java'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Barcode image generation library in Java'
                url siteUrl

                organization {
                    name 'Pnuema Productions'
                    url baseUrl
                }

                issueManagement {
                    system 'GitHub'
                    url siteUrl + '/issues'
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

                licenses {
                    license {
                        name "MIT License"
                        url "https://opensource.org/licenses/MIT"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "barnhill"
                        name "Brad Barnhill"
                        email "bradfordbarnhill@gmail.com"
                    }
                }
            }
        }
    }
}
